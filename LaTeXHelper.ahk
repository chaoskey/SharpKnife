;@Ahk2Exe-SetProductName    LaTexåŠ©æ‰‹
;@Ahk2Exe-SetProductVersion 2021.11.29
;@Ahk2Exe-SetDescription LaTexåŠ©æ‰‹
;@Ahk2Exe-SetFileVersion    2021.11.29
;@Ahk2Exe-SetCopyright @2021-2025
;@Ahk2Exe-SetLanguage 0x0804
;@Ahk2Exe-SetOrigFilename LaTexHelper
;@Ahk2Exe-SetLegalTrademarks chaoskey
;@Ahk2Exe-SetCompanyName chaoskey

; ----------------------------------------------
; å‚è€ƒKatexï¼Œå°½å¯èƒ½ä½¿ç”¨latexè§¦å‘å‡ºå¯¹åº”çš„unicodeå­—ç¬¦
;
; https://katex.org/docs/supported.html
; 
; åªå¯¹ä¸æ–¹ä¾¿é”®ç›˜è¾“å…¥çš„å­—ç¬¦è¿›è¡Œlatex[TAB]æ›¿æ¢ï¼Œ å¦‚æœæ²¡æœ‰æ›¿æ¢è¯´æ˜è¾“å…¥é”™è¯¯æˆ–ä¸æ”¯æŒ
;
; åªæ”¯æŒå•å­—ç¬¦çš„latexè§¦å‘ï¼ˆç›®å‰æ”¯æŒå¦‚ä¸‹6ç±»ï¼‰
;    1.ä¸‹æ ‡   _n[TAB]             â‚™   ã€ä¸‹æ ‡è§¦å‘ã€‘
;    2.ä¸Šæ ‡   ^n[TAB]             â¿   ã€ä¸Šæ ‡è§¦å‘ã€‘
;    3.å•å­—ç¬¦  \alpha[TAB]         Î±   ã€å•å­—ç¬¦è§¦å‘ã€‘
;    4.å­—ä½“     \mathbbR[TAB]       â„   ã€ç©ºå¿ƒå­—ç¬¦è§¦å‘ã€‘
;    4.å­—ä½“     \mathfrakR[TAB]     â„œ   ã€Frakturå­—ç¬¦è§¦å‘ã€‘
;    4.å­—ä½“     \mathcalR[TAB]      ğ“¡   ã€èŠ±ä½“å­—ç¬¦è§¦å‘ã€‘  
;    5.ç»„åˆ     R\[ç»„åˆå­—ç¬¦][TAB] æ¯”å¦‚R\dot[TAB]          RÌ‚   ã€ç»„åˆå­—ç¬¦è§¦å‘ã€‘
;    6.ç‰‡æ®µæœç´¢     \[ç‰‡æ–­å­—ç¬¦ä¸²][TAB]       ã€æœç´¢å­—ç¬¦è§¦å‘ã€‘
; ----------------------------------------------
#SingleInstance, force

FileEncoding , UTF-8

#Include %A_ScriptDir%\lib\LaTeXs.ahk

; åŠ è½½çƒ­LaTeX
loadHotlatex()
; åˆ›å»ºè§¦å‘çƒ­é”®
loadTriggerHotKey()
return ; è‡ªåŠ¨è¿è¡Œæ®µç»“æŸ

;------------------------------------------------------------------------------------------------------
;        latexçƒ­é”®å¤„ç† (å®Œå…¨ç”¨Inputæ¥ç®¡)
;------------------------------------------------------------------------------------------------------
; æŒ‰ä¸‹\é”®ï¼Œç­‰å€™è¾“å…¥ï¼Œç„¶åtabï¼Œå¯èƒ½å‡ºç°å¦‚ä¸‹4ç§æƒ…å†µ
;     1) \åå¦‚æœè¾“å…¥å°‘äº2ä¸ªå­—ç¬¦ï¼Œå°è¯•å®Œå…¨åŒ¹é…ï¼Œå¦åˆ™æ— ä»»ä½•å˜åŒ–
;     2) å¦‚æœå®Œå…¨åŒ¹é… æˆ– ä¸å®Œå…¨ä½†å”¯ä¸€åŒ¹é…ï¼Œä¼šè‡ªåŠ¨æ›¿æ¢æˆ:  unicodeï¼ˆunicodeæ¨¡å¼ï¼‰ 
;                                                  æˆ– æ­£ç¡®çš„latexä»£ç ï¼ˆlatexåŠ©æ‰‹æ¨¡å¼ï¼‰
;     3) å¦‚æœä¸å®Œå…¨ä¸”ä¸å”¯ä¸€åŒ¹é…ï¼Œä¼šå¼¹å‡ºèœå•é€‰æ‹©æ›¿æ¢ï¼Œæ›¿æ¢çš„ç»“æœæ˜¯: unicodeå­—ç¬¦ï¼ˆunicodeæ¨¡å¼ï¼‰
;                                                          æˆ– æ­£ç¡®çš„latexä»£ç ï¼ˆlatexåŠ©æ‰‹æ¨¡å¼ï¼‰
;     4) å¦‚æœä¸åŒ¹é…ï¼Œä¸åšä»»ä½•å¤„ç†
;
;  å¯ç”¨`Win + \`  è¿›è¡Œ unicodeæ¨¡å¼ / latexåŠ©æ‰‹æ¨¡å¼ åˆ‡æ¢  ã€ä¼šæœ‰1såæ¶ˆå¤±çš„æç¤ºã€‘
;       unicodeæ¨¡å¼:   è¾“å‡ºçš„ç»“æœæ˜¯unicodeå­—ç¬¦ï¼Œæ¯”å¦‚ â¨
;       latexåŠ©æ‰‹æ¨¡å¼: å¦‚æœè¾“å…¥æ­£ç¡®çš„æˆ–å®Œå…¨ä¸æ­£ç¡®ï¼Œæ²¡æœ‰ä»»ä½•ååº”
;                     å¦‚æœè¾“å…¥çš„æ­£ç¡®çš„ç‰‡æ®µï¼ˆä¸å®Œå…¨æ­£ç¡®ï¼‰ï¼Œä¼šå¼¹å‡ºèœå•ï¼Œé€‰æ‹©è¾“å…¥ï¼Œæ¯”å¦‚: \bigoplus
;------------------------------------------------------------------------------------------------------
HotlatexHandler()
{
    prefix := A_ThisHotkey

    ; é»˜è®¤0(æˆ–æœªèµ‹å€¼): å¯¹åº”latexåŠ©æ‰‹æ¨¡å¼;  1: å¯¹åº”unicodeæ¨¡å¼ 
    global unicodeMode
    if (not unicodeMode){
        unicodeMode := 0
    }

    ; å¦‚æœå·²ç»åŠ è½½im_switchæ¨¡å—ï¼Œæ”¯æŒåœ¨ä¸­æ–‡è¾“å…¥çŠ¶æ€ä¸‹ç›´æ¥è¾“å…¥ï¼Œä¼šè‡ªåŠ¨åˆ‡æ¢å€’è‹±æ–‡çŠ¶æ€
    _getImState := "getImState"
    _setImState := "setImState"
    _IMToolTip := "IMToolTip"
    if isFunc(_getImState) and (%_getImState%() = 1)
    {
        %_setImState%(0)
        ; ç¡®ä¿åªåœ¨ä¸­æ–‡çŠ¶æ€ä¸‹åŠ¨ä½œ
        prefix := LTrim(prefix, "~")
        ; _ åœ¨å¾®è½¯ä¸­æ–‡ä¸‹æ˜¾ç¤º â€”â€”(ä¸¤ä¸ªå­—ç¬¦)
        ; ^ åœ¨å¾®è½¯ä¸­æ–‡ä¸‹æ˜¾ç¤º â€¦â€¦(ä¸¤ä¸ªå­—ç¬¦)
        ; \ åœ¨å¾®è½¯ä¸­æ–‡ä¸‹æ˜¾ç¤º ã€(ä¸€ä¸ªå­—ç¬¦)
        ; : åœ¨å¾®è½¯ä¸­æ–‡ä¸‹æ˜¾ç¤º ï¼š(ä¸€ä¸ªå­—ç¬¦)
        ; $ åœ¨å¾®è½¯ä¸­æ–‡ä¸‹æ˜¾ç¤º ï¿¥(ä¸€ä¸ªå­—ç¬¦)
        ; æ‰€ä»¥ï¼Œ_å’Œ^æœ‰ç‚¹ç‰¹æ®Šï¼Œéœ€è¦é€€ä¸¤æ ¼
        nBS := 1
        if (prefix == "_") or (prefix == "^") {
            nBS := 2
        }
        Send ^{Space}{BS %nBS%}{text}%prefix%
        %_IMToolTip%()
    }

    ;--------------------------------------------
    ;        é¦–å…ˆè§£å†³è§¦å‘çƒ­é”®ç»„åˆçš„ç›¸äº’å¹²æ‰°é—®é¢˜
    ;--------------------------------------------

    ; æŒ‰æ¬¡åºè®°å½•prefix å’Œ searchï¼Œé˜²æ­¢ç±»ä¼¼ â€œ_\â€ çš„ç»„åˆç›¸äº’å¹²æ‰°
    global  prefixs
    global  searchs
    global  indexPrefix
    global  lastErrorLevel

    ; æ¯”å¦‚: å¦‚æœè¾“å…¥â€œ_abc\efgâ€, _å…ˆç­‰å€™è¾“å…¥ï¼Œ \åç­‰å€™è¾“å…¥
    if (not prefixs)
    {
        prefixs := []
        indexPrefix := 0
    }
    prefixs.Push(prefix)
    indexPrefix := indexPrefix + 1
    
    ; ç­‰å€™è¾“å…¥
    Input, search, V C , {tab}{space}{enter}{esc}{F1}{F2}{F3}{F4}{F5}{F6}{F7}{F8}{F9}{F10}{F11}{F12}{Up}{Down}{Home}{End}{PgUp}{PgDn}{CapsLock}{NumLock}{PrintScreen}{Pause}
    if (not lastErrorLevel)
        lastErrorLevel := ErrorLevel

    ; æ¯”å¦‚: å‰é¢è¾“å…¥â€œ_abc\efgâ€, ç„¶åtab, _å…ˆå¤„ç†efgï¼Œ åå¤„ç†abc\
    ; åå¤„ç†çš„æ’å‰é¢ï¼Œç¡®ä¿å’Œprefixså¯¹åº”
    if not searchs
        searchs := []
    searchs.InsertAt(1, search)

    if (indexPrefix > 1) and (prefixs[indexPrefix] == prefix)
    {
        ; å¿½ç•¥åç»­è§¦å‘ï¼ŒåŒæ—¶ç¡®ä¿äº†searchså®Œå…¨å¡«å……
        indexPrefix := indexPrefix - 1
        return
    }

    ; æ‹¼å‡‘æˆå®Œæ•´çš„å¾…å¤„ç†å­—ç¬¦ä¸²
    search := prefixs[1]
    for index, value in searchs
        search := search "" value

    ; ä¸ºåé¢çš„æ­£åˆ™è¡¨è¾¾å¼å‡†å¤‡
    trigStr := ""
    for i_, v_ in getTriggerFirstChar(){
        trigStr := trigStr "\" v_
    }
    ; ç¡®å®šæœ€åä¸€ä¸ªå‰ç¼€-æœç´¢å¯¹ï¼ˆä¸€å®šä¼šåŒ¹é…ï¼‰
    regStr := "O)([" trigStr "]+)([^" trigStr "]*)$"
    if (not unicodeMode)
        ; latexåŠ©æ‰‹æ¨¡å¼ä¸‹ï¼Œç¦ç”¨ç»„åˆè§¦å‘
        regStr := "O)([" trigStr "])([^" trigStr "]*)$"
    RegExMatch(search, regStr, SubPat)
    prefix := SubPat.Value(1)
    search := SubPat.Value(2)

    ; æ¸…ç©ºå…¨å±€å˜é‡
    prefixs := []
    searchs := []
    indexPrefix := 0
    ; étabç»ˆæ­¢ç¬¦è§¦å‘ï¼Œè¡¨ç¤ºæ”¾å¼ƒ
    if (lastErrorLevel != "EndKey:tab")
    {
        lastErrorLevel := ""
        return
    }
    lastErrorLevel := ""

    ;--------------------------------------------
    ;        ä¸‹é¢æ˜¯æ­£å¼çš„é€»è¾‘æµç¨‹
    ;--------------------------------------------
    latexHotstring := getLaTeXHotstring() 

    ; éœ€è¦åˆ é™¤çš„å­—ç¬¦æ•°:  å‰ç¼€æ•° + è¾“å…¥å­—ç¬¦æ•° + `tå­—ç¬¦æ•°
    n := StrLen(prefix) + StrLen(search) + 1

    flag := False ; é»˜è®¤æ˜¯ä¸å®Œå…¨åŒ¹é…æ¨¡å¼
    ; å¦‚æœè¾“å…¥çš„å­—ç¬¦æ•°å°äº2ç„¶å[tab]ï¼Œåˆ™è¦æ±‚å®Œå…¨åŒ¹é…ï¼ˆé˜²æ­¢èœå•è¿‡é•¿ï¼‰
    if (n < StrLen(prefix) + 2 + 1)
        flag := True

    ; æœç´¢åŒ¹é…çš„ç´¢å¼•
    global matches := []
    for index, value in latexHotstring
    {
        ; å‰ç¼€ä¸åŒ¹é…ï¼Œç›´æ¥è·³è¿‡
        if (InStr(value, prefix) != 1) {
            Continue
        }

        ; å‰”é™¤å‰ç¼€åçš„éƒ¨åˆ†ä½œä¸ºâ€œé”®â€
        key := SubStr(value, StrLen(prefix)+1) 
        value := getUnicode(index)

        if unicodeMode and InStr(value, key)
            ; Hotlatex("\mathbb", "é»‘æ¿ç²—ä½“ â„ \mathbb{R}")
            ; å¦‚æœâ€œé”®â€åœ¨â€œå€¼â€ä¸­å‡ºç°ï¼Œè¡¨æ˜åªé€‚ç”¨äºlatexåŠ©æ‰‹æ¨¡å¼. ã€ç¦æ­¢â€œå€¼â€ä»¥:å¼€å¤´ã€‘
            Continue

        if (not unicodeMode) and (SubStr(value, 1, 1) == ":")
            ; Hotlatex("\mathbba", ":ğ•’") 
            ; å¦‚æœ"å€¼"ä»¥:å¼€å¤´ï¼Œè¡¨æ˜åªé€‚ç”¨äºunicodeæ¨¡å¼. ã€ç¦æ­¢â€œé”®â€åœ¨â€œå€¼â€ä¸­å‡ºç°ã€‘
            Continue
        

        ; å¦‚æœæ˜¯å®Œå…¨åŒ¹é…ï¼Œè·³è¿‡å­—ç¬¦æ•°è¿‡å¤§çš„éƒ¨åˆ†ï¼ˆåŸºäºå·²æ’åºçš„æƒ…å†µï¼‰
        if flag and (StrLen(prefix)+StrLen(key) > n-1)
            Break
        
        if  InStr(key, search) {
            if (search == key) {
                if (Not flag)
                {
                    ; è¿›å…¥å®Œå…¨åŒ¹é…æ¨¡å¼
                    matches := []
                    flag := True
                }    
                ; æ”¶é›†å®Œå…¨åŒ¹é…çš„çƒ­LaTeXç´¢å¼•
                matches.Push(index)
            }else if (Not flag) {
                ; åœ¨ä¸å®Œå…¨åŒ¹é…æ¨¡å¼ä¸‹ï¼Œæ‰èƒ½æ”¶é›†ä¸å®Œå…¨åŒ¹é…çš„çƒ­LaTeXç´¢å¼•
                matches.Push(index)
            }
        }    
    }

    ; å”¯ä¸€åŒ¹é…ï¼ˆå¯èƒ½æ˜¯å®Œå…¨åŒ¹é…ï¼Œä¹Ÿå¯èƒ½æ˜¯ä¸å®Œå…¨åŒ¹é…ï¼‰
    if (matches.Length() == 1)
    {
        latexBlock := getLaTeXBlock(matches[1])
        if (not unicodeMode) and (latexBlock=="") {
            ; latexåŠ©æ‰‹æ¨¡å¼ and életexå—
            if flag {
                ; å®Œå…¨åŒ¹é…
                Send, {bs}
            }else{
                ; ä¸å®Œå…¨åŒ¹é…
                value := getLaTeXHot(matches[1])
                Send, {bs %n%}%value%
            }
            return
        }
        if (not unicodeMode) and latexBlock {
            ; latexåŠ©æ‰‹æ¨¡å¼ and letexå—
            latexBlock := "{bs " n "}" latexBlock  
            for i_, v_ in StrSplit(latexBlock, "##") {
                Send, %v_%
            }
            return
        }
        ; unicodeæ¨¡å¼, å‰”é™¤é¢å¤–æ ‡è®°â€œ:â€
        value := LTrim(getUnicode(matches[1]), ":")
        Send, {bs %n%}%value%        
        return
    } 

    ; ä¸å”¯ä¸€åŒ¹é…ï¼ˆè‚¯å®šæ˜¯ä¸å®Œå…¨åŒ¹é…ï¼Œéœ€è¦å¼¹å‡ºèœå•ï¼Œé€šè¿‡é€‰æ‹©è¿›è¡Œæ›¿æ¢ï¼‰
    if (matches.Length() > 1)
    {
        ; å¼¹å‡ºèœå•
        for index, value in matches
        {
            ; é€šè¿‡æ·»åŠ åç¼€æ¥è¡¨ç¤ºçƒ­LaTeXç´¢å¼•,  Unicodeå‰”é™¤é¢å¤–æ ‡è®°â€œ:â€
            itemName := getLaTeXHot(value) "=" LTrim(getUnicode(value), ":")
            Menu, HotMenu, Add, %itemName%, MenuHandler
        }
        Send, {bs %n%}
        Sleep 30 ; å»¶è¿Ÿ30æ¯«ç§’ï¼Œç¡®ä¿å¼¹å‡ºçª—å£å‰é€€æ ¼å®Œæˆï¼ˆä¼¼ä¹æ²¡æœ‰åŒæ­¥å‘é€çš„APIï¼Œåªèƒ½è¿™æ ·ï¼‰
        Menu, HotMenu, Show
        return
    }

    ; ä¸åŒ¹é…ï¼Œ åªéœ€è¦é€€1æ ¼å¤åŸ
    Send, {bs}
}

MenuHandler(){    ; èœå•é€‰æ‹©å¤„ç†
    global matches
    global unicodeMode

    ; è·å–å¯¹åº”èœå•é¡¹ç›¸å¯¹çƒ­latexæ•°ç»„çš„ç´¢å¼•
    index := matches[A_ThisMenuItemPos]
    ; çƒ­LaTeX LaTeXå— Unicode
    latex := getLaTeXHot(index)
    unicdoe := LTrim(getUnicode(index), ":")  ; å‰”é™¤é¢å¤–æ ‡è®°â€œ:â€
    latexBlock := getLaTeXBlock(index)
    
    if unicodeMode {
        Send, %unicdoe%
    }else if latexBlock {
        ; é€šè¿‡â€œ##â€å°†{text}å’Œé{text}åˆ†æˆä¸åŒéƒ¨åˆ†ï¼Œç„¶åè¿ç»­æ‰§è¡Œ
        for i_, v_ in StrSplit(latexBlock, "##") {
            Send, %v_%
        }
    }else{
        Send, %latex%
    }
    Menu, HotMenu, DeleteAll
    return
}

loadTriggerHotKey()
{
    ; åŠ¨æ€åˆ›å»ºè§¦å‘çƒ­é”®
    ; https://www.autoahk.com/help/autohotkey/zh-cn/docs/commands/Hotkey.htm
    for i_ , v_ in getTriggerFirstChar() {
        ; ~ è¡¨ç¤ºè§¦å‘çƒ­é”®æ—¶, çƒ­é”®ä¸­æŒ‰é”®åŸæœ‰çš„åŠŸèƒ½ä¸ä¼šè¢«å±è”½(å¯¹æ“ä½œç³»ç»Ÿéšè—)
        Hotkey, ~%v_%, HotlatexHandler
    }
}

;-----------------------------------------------------------------------------
;    `Win + \`  è¿›è¡Œ unicodeæ¨¡å¼ / latexåŠ©æ‰‹æ¨¡å¼ åˆ‡æ¢
;-----------------------------------------------------------------------------

#\::    ; æ¨¡å¼åˆ‡æ¢çƒ­é”®
global unicodeMode
if (not unicodeMode){
    unicodeMode := 0
}
unicodeMode := Mod(unicodeMode+1,2)
if unicodeMode {
    ToolTip, unicodeæ¨¡å¼
}else{
    ToolTip, latexåŠ©æ‰‹æ¨¡å¼
}
SetTimer, RemoveLatexToolTip, -1000
return

RemoveLatexToolTip:    ; åˆ é™¤å½“å‰æ¨¡å¼æç¤º
ToolTip
return

